Author: Breno G. de Oliveira <garu@cpan.org>
Title: Santa’s Workshop Secrets: The Magical Test2 Suite
Topic: Testing

=encoding utf8

Ho, ho, ho, dear friends! Tonight I have a little behind-the-scenes to share with you directly from the North Pole. As you all know, this season is pretty busy for me and the elves. We have to make sure every letter is received, assigned and fulfilled, and that every present is in tip-top condition and delivered on time. So, of course, our Christmas operation needs to be tested thoroughly.

But writing good tests can sometimes be a coal in the stocking. Some things are hard to test, and even though Perl has many nice testing modules to help with that, I can never quite remember their names or which to use, and together they can add so many external dependencies that we mostly keep ourselves to Test::More.

Well, not anymore. This year we updated all our test code to use the magical L<Test2-Suite|https://metacpan.org/pod/Test2::V0>. A single distribution that updates and replaces not just Test::More, but many other testing modules. And the best part? It will be a core distribution in Perl 5.40 onwards! Talk about a Christmas Miracle <3

The basics work pretty much like Test::More, so if you’re used to it you’ll feel right at home. In fact, if you’re not doing anything fancy you can probably replace “use Test::More” with “use Test2::V0” and everything will work just fine. Check it out:

    use Test2::V0;

    use Acme::Christmas;

    ok my $xmas = Acme::Christmas->new, 'able to instantiate object';
    isa_ok $xmas, 'Acme::Christmas';
    can_ok $xmas, qw( read_letters make_toys );

    is $xmas->date, 'December, 25th', 'got the right date';

    note "let’s see if the Grinch is close";
    if (grinch()) {
        fail 'oh, noes…';
    } else {
        pass 'coast is clear!';
    }

    SKIP: {
        skip "tests for winter only", 1 unless $xmas->is_winter;
        like $xmas->carol, qr/Merry/, ‘found the proper lyrics’;
    }

    done_testing; # you can also “plan N;” if you prefer to count your tests.

But you’ll also get some nice cranberry sauce right out of the box.
For starters, C<strict> and C<warnings> are on by default (and don’t worry,
you can easily disable this behavior if you want). Second, remember how
you always wanted tests to print more useful debug data when they fail?
Oh, how common it was to write things like C<< is $x, 'x' or diag "debug data here" >>, or even wrap tests under an C<if()> clause. Now, Test2::V0’s C<is()>, C<ok()>, C<like()> and most other test functions support extra
arguments I<after the test description>, so you can write them as:

    is $x, 'x', 'test the value of x', @debug_strings;

and the extra output will be printed only when that test fails. Ho! Ho! Ho!

=head2 The all-powerful “is” and “like”

This is straight out my favorite feature. You may have noticed I did not
include “C<is_deeply()>” on the list of compatibility with Test::More.
Well, that’s because there is no need for it. That’s right! If the variable
you’re testing is a data structure, you can simply use C<is()> and it will
do a deep check, failing if values don’t match or if anything is missing:

    is $recipe, {
        name => 'Fruitcake',
        ingredients => {
            eggs => 5,
            flour => 3,
            'dried fruit' => ['cherries', 'apricots', 'dates'],
        },
    }, 'got proper dessert!';

What about that time of the year when you get a data structure or object
and care only about a few keys, items or attributes? Ooh, Santa’s got a
gift for you, too! B<< Use like() with the nested structure to ignore any keys/positions you haven’t defined >> in a true non-strict (partial) match.
It even lets you mix and match between exact values (by passing a string
or a number) and values that match a regular expression (by providing
the regexp).

    like $recipe, {
        name => qr(cake)i,  # must contain ‘cake’ (case insensitive)
        ingredients => {
            eggs => qr(\d+),   # any number
            flour => 5,            # exactly 5
            dried fruit’ => [qr/cherr(y|ies)/, 'apricots', qr/dates?/],  # mix and match!
        }
    }, 'partial match in nested variables, mixing is() and like() at any level';
```

Think the presents are over? Think again! For even more complex validations you can check your variable against a builder (and there are L<< many builders available for hashes, arrays, objects, etc|https://metacpan.org/pod/Test2::Tools::Compare#VALUE-SPECIFICATIONS >>). For example, let’s say I wanted
to check whether C<$recipe> has a name and ingredients, and if one of the
dried fruits is raisins. Also, just to make it a little harder, let’s
make sure it has no key called ‘microwave’. To do all that, we just write
a very simple definition of our partial hash containing only the bits we
care about:

    use Test2::V0 qw( is hash field bag item etc L DNE ); # import everything we use in this test

    is $recipe, hash {
      field name => L;  # the value of the 'name' key is defined and has a L()ength.
      field ingredients => hash {
        # 'bag' is an 'array' that doesn't care about element order.
        field 'dried fruit' => bag { item 'raisins'; etc; };
        etc;
      };
      field microwave => DNE; # the 'microwave' key Does Not Exist.
      etc;    # ignore other keys. Use 'end' to fail the test if other keys exist.
    }, 'partial match from a generated definition!';

If that wasn’t impressive enough, here are some extra nice ways to make your tests more thorough, robust and clear without having to load external modules or fiddle with the symbol table:

=head2 Test if loading a module imports (or doesn’t import) a function or a variable:

    use Some::Module;
    imported_ok 'mysub', '$myvar', '@myothervar';
    not_imported_ok 'othersub', '$othervar';
```

=head2 Test if something warns or dies / raises an exception

    like dies { … }, qr/some error/, ‘got expected exception from block’;
    ok lives { … }, ‘code lived!’, “oh, noes! Died with error ‘$@’”;

    ok warns { … }, ‘at least one warning was issued in the block’;
    is warns { … }, 2, ‘got the right number of warnings in block’;

    is warnings { … }, [
        qr/first warning issued/,    # lax match
        ‘second warning issued in somefile.pl line 10’,  # strict match,
    ], ‘matched expected warning messages’;

=head2 Stop and bail out of testing whenever a single test fails:

If you add this to the beginning of your test file, it will die and stop testing that file as soon as any test on that file fails:


    use Test2::Plugin::DieOnFail;

If you’re running a bunch of different test files, it will not stop testing altogether, just that particular file. To truly bail out of all testing as soon as any test on a file fails, do this instead:

    use Test2::Plugin::BailOnFail;

If you just want to bail on a single test in the file, use "C<< ... or bail_out($reason) >>" after the test.

=head2 Skip tests unless we have a specific perl or module version available:

    # skip all tests in file unless perl is v5.38 or greater:
    use Test2::Require::Perl 'v5.38';

    # skip all tests in file unless ‘Some::Module version 2.34 or greater is available.
    # omit the version if you only care about whether the module is available or not.
    use Test2::Require::Module ‘Some::Module' => '2.34'


=head2 Easily mocking objects and classes

I saved the best for last. Sometimes we need to mock classes, objects or methods in order to properly test others that interact with them. With the Test2 Suite, this has never been easier.

For starters, let’s say you want to create an object that will be passively called by whatever it is you’re testing:

    my $obj = mock;

Now, C<$obj> is a dud that will accept any method calls and return C<undef>. What about if you want it to return something specific?

    my $obj = mock { merry => 'christmas!' };  # you can use “sub {...}” for complex return values.

    is $obj->merry, ‘christmas!’, ‘my mock works’;

When you need to test chained calls, you can always nest your mocks():

    my $obj = mock { happy => mock { holidays => mock { everyone => ‘Ho! Ho! Ho! } } };

    is $obj->happy->holidays->everyone, ‘Ho! Ho! Ho!’, ‘chained mocks’;

I<“But Santa”>, you may ask, I<< “What about when the class is instantiated and used somewhere inside the code I want to test? How can I use mocks to add or override methods to something out of my hands?” >> No worries! Just expand your mock definition for more control:

    my $mock_meta = mock 'Some::Class' => (
        track => true,
        override => [
            some_method => sub { … },
            other_method => sub { … },
        ],
        add => [
            new_method => sub { … },
        ],
    );

And now, as long as C<$mock_meta> exists, any instances of “C<Some::Class>” will have the mocked behavior. To restore them back to the original, simply undefine that C<$mock_meta> variable or call C<< $mock_meta->reset_all() >>.

One thing you may have noticed in the example above is the “C<< track => true >>”. That means C<$mock_meta> will contain a lot of information ready for you to inspect regarding how many times any given method was called, and which arguments were used. This is particularly important to check if the code you’re testing is properly invoking the mocked class.

    test_something_that_uses_my_mocked_class();
    is(
        $mock_meta->sub_tracking->{some_method}->@*,
        1,
        'some_method() called only once!'
    );

    like(
        $mock_meta->sub_tracking->{some_method}[0]{args},
        [ qr(), ‘arg1’, qr(arg2) ],
        'testing call arguments for method “some_method”'
    );

    $mock_meta->clear_sub_tracking(); # so we can go again in isolation


Note that to get the mock metadata from a given variable holding an object, you can also do:

        my ($mock_meta) = mocked $actual_obj;

So there you have it. With the magical powers of Test2, when Christmas Eve arrives, I’ll take off on my sleigh with confidence, knowing that every toy has passed its tests. This means more smiles, laughter, and Christmas joy for people all around the world. Give it a try, too!

I<< – Santa, out. >>
